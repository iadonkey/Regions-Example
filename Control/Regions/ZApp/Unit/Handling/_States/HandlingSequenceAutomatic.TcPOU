<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="HandlingSequenceAutomatic" Id="{7b948839-0d31-4b50-8d53-d7d203650277}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK HandlingSequenceAutomatic EXTENDS HandlingSequence IMPLEMENTS ZCore.ISequence
VAR
  _step : ZCore.Step(HandlingStep.AutomaticBegin, HandlingStep.AutomaticEnd);
  _timer : ZAux.Timer;
  _rand : ZAux.RandomUniform(seed:=4, low:=1, high:=4);
  
  _newTargetZone, _currentTargetZone : INT;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT Busy THEN
  RETURN;
END_IF

IF OnStart(_step) THEN
  ; // init custom sequence variables here ...
END_IF

IF OnStop() THEN
  RETURN; // external stop was triggered
END_IF

IF OnHalt() THEN
  ; // Halting := TRUE; // Uncomment to acknowledge that the sequence is halting
END_IF

REPEAT
  LogStep();

  CASE _step.Index OF
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticBegin:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        ;
      END_IF

      // Await(obj1:=, nextStep:=HandlingStep.AutomaticEnd);
      _step.SetNext(HandlingStep.AutomaticWait);
    
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticWait:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _timer.WaitAsync(0.3);
      END_IF

      IF _timer.Done
      THEN
      
        WHILE _newTargetZone = _currentTargetZone
        DO
          _newTargetZone := LIMIT(1, LREAL_TO_INT(ZPlatform.Floor(_rand.Next())) , 3); 
        END_WHILE
        
        _currentTargetZone := _newTargetZone;
        
        _step.SetNext(HandlingStep.AutomaticMove);
      END_IF
      
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticMove:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _axis.AxisX.MoveAbsoluteAsync(THIS^, position := TargetPosition(_currentTargetZone), speed := 300);
      END_IF
      
      Await(obj1:=_axis.AxisX, nextStep:=HandlingStep.AutomaticWait);
    
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticEnd:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      SetBusy(FALSE);
  
  ELSE
    Abort('sequence contains unhandled step');
  END_CASE
UNTIL _step.IsNotRepeatable() OR_ELSE NOT Busy END_REPEAT]]></ST>
    </Implementation>
    <Method Name="TargetPosition" Id="{17c07962-53eb-48cd-a8cc-ad69f116e039}">
      <Declaration><![CDATA[METHOD TargetPosition : LREAL
VAR_INPUT
  zone : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE zone
OF
  1: TargetPosition := 50;
  2: TargetPosition := 200;
  3: TargetPosition := 400;  
END_CASE]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>