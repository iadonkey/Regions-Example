<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="HandlingSequenceAutomatic" Id="{7b948839-0d31-4b50-8d53-d7d203650277}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK HandlingSequenceAutomatic EXTENDS HandlingSequence IMPLEMENTS ZCore.ISequence
VAR
  _step : ZCore.Step(HandlingStep.AutomaticBegin, HandlingStep.AutomaticEnd);
  _timer : ZAux.Timer;
  _rand : ZAux.RandomUniform(seed:=4, low:=1, high:=4);
  
  _speed : LREAL := 100;
  _path : Path1d;
  _newTargetZone, _currentTargetZone : INT;
  _sortedRegions : ARRAY[0..2] OF ZApplication.IRegion;
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[IF NOT Busy THEN
  RETURN;
END_IF

IF OnStart(_step) THEN
  ; // init custom sequence variables here ...
END_IF

IF OnStop() THEN
  RETURN; // external stop was triggered
END_IF

IF OnHalt() THEN
  Halting := TRUE;
END_IF

REPEAT
  LogStep();

  CASE _step.Index OF
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticBegin:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        ;
      END_IF

      _step.SetNext(HandlingStep.AutomaticWait);
      
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticWait:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _timer.WaitAsync(0.3);
      END_IF

      IF Halting
      THEN
        _step.SetNext(HandlingStep.AutomaticEnd);
        
      ELSIF _timer.Done
      THEN
      
        WHILE _newTargetZone = _currentTargetZone
        DO
          _newTargetZone := LIMIT(1, LREAL_TO_INT(ZPlatform.Floor(_rand.Next())) , 3); 
        END_WHILE
        
        _currentTargetZone := _newTargetZone;
        _step.SetNext(HandlingStep.AutomaticReservePath);
      END_IF      
    
    (* -------------------------------------------------------------------------------------------------------------------------------- *)    
    HandlingStep.AutomaticReservePath:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _path
          .WithSortedRegions(ADR(_sortedRegions), 3)
          .WithWidth(50)
          .WithSafetyMargin(_speed * 0.01 * 10) // 10 cycles safety margin
          .FromPosition(_axis.AxisX.ActualPosition)
          .ToPosition(TargetPosition(_currentTargetZone))
          .ReserveAsync(_parent);          
      END_IF
      
      _path.Cyclic();

      Await(_path, HandlingStep.AutomaticMoveThroughRegion); 
    
    (* -------------------------------------------------------------------------------------------------------------------------------- *)    
    HandlingStep. AutomaticMoveThroughRegion:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)    
      IF _step.OnEntry()
      THEN
        _axis.AxisX.MoveAbsoluteAsync(THIS^, _path.CurrentEdge, _speed);
      END_IF
      
      IF _path.RegionsToPass = 0
      THEN
        _step.SetNext(HandlingStep.AutomaticMoveToEndPosition);
      ELSIF _path.CheckAndLock(_axis.AxisX.ActualPosition)
      THEN
        _path.IncrementRegion(); 
        _step.SetNext(HandlingStep.AutomaticMoveThroughRegion);
      END_IF

      Assert(_axis.AxisX);
        
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticMoveToEndPosition:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      IF _step.OnEntry()
      THEN
        _axis.AxisX.MoveAbsoluteAsync(THIS^, position := TargetPosition(_currentTargetZone), speed := _speed);
      END_IF
      
      Await(obj1:=_axis.AxisX, nextStep:=HandlingStep.AutomaticWait);
    
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
    HandlingStep.AutomaticEnd:
    (* -------------------------------------------------------------------------------------------------------------------------------- *)
      SetBusy(FALSE);
  
  ELSE
    Abort('sequence contains unhandled step');
  END_CASE
UNTIL _step.IsNotRepeatable() OR_ELSE NOT Busy END_REPEAT]]></ST>
    </Implementation>
    <Method Name="TargetPosition" Id="{17c07962-53eb-48cd-a8cc-ad69f116e039}">
      <Declaration><![CDATA[METHOD TargetPosition : LREAL
VAR_INPUT
  zone : INT;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE zone
OF
  1: TargetPosition := 50;
  2: TargetPosition := 200;
  3: TargetPosition := 400;  
END_CASE]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>